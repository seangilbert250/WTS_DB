USE WTS
GO

CREATE TABLE #WORKITEM_TASKS(
	BUGTRACKER_ID int, BT_TASKID int
	, BT_CREATED_USERNAME nvarchar(50), CREATED_FIRST_NAME nvarchar(60), CREATED_LAST_NAME nvarchar(60)
	, BT_UPDATED_USERNAME nvarchar(50), UPDATED_FIRST_NAME nvarchar(60), UPDATED_LAST_NAME nvarchar(60)	
	, BT_USERNAME nvarchar(50), FIRST_NAME nvarchar(60), LAST_NAME nvarchar(60)	
	, PLANNEDSTARTDATE datetime, ACTUALSTARTDATE datetime, ACTUALENDDATE datetime, PLANNEDHOURS int, ACTUALHOURS int, COMPLETIONPERCENT int
	, BT_STATUS nvarchar(60), TITLE nvarchar(150), [DESCRIPTION] nvarchar(500), ARCHIVE bit, SORT_ORDER int
	, CREATEDDATE datetime, UPDATEDDATE datetime
	)
INSERT INTO #WORKITEM_TASKS
	--BT TASKS ASSIGNED TO BT ITEM
	SELECT 
		bt.tsk_bug AS BUGTRACKER_ID
		, bt.tsk_id AS BT_TASKID
		, cu.us_username AS BT_CREATED_USERNAME
		, cu.us_firstname AS CREATED_FIRST_NAME
		, cu.us_lastname AS CREATED_LAST_NAME
		, lu.us_username AS BT_UPDATED_USERNAME
		, lu.us_firstname AS UPDATED_FIRST_NAME
		, lu.us_lastname AS UPDATED_LAST_NAME
		, au.us_username AS BT_USERNAME
		, au.us_firstname AS FIRST_NAME
		, au.us_lastname AS LAST_NAME
		, bt.tsk_planned_start_date AS PLANNEDSTARTDATE
		, bt.tsk_actual_start_date AS ACTUALSTARTDATE
		, bt.tsk_actual_end_date AS ACTUALENDDATE
		, bt.tsk_planned_duration AS PLANNEDHOURS
		, bt.tsk_actual_duration AS ACTUALHOURS
		, ISNULL(bt.tsk_percent_complete,0) as COMPLETIONPERCENT
		, st.st_name AS BT_STATUS
		, LEFT(bt.tsk_description, 150) AS TITLE
		, bt.tsk_description AS [DESCRIPTION]
		, 0 AS ARCHIVE
		, bt.tsk_sort_sequence AS SORT_ORDER
		, bt.tsk_created_date AS CREATEDATE
		, bt.tsk_last_updated_date AS UPDATEDDATE
	FROM
		BugTracker.dbo.bug_tasks bt
			LEFT JOIN BugTracker.dbo.users cu ON bt.tsk_created_user = cu.us_id
			LEFT JOIN BugTracker.dbo.users lu ON bt.tsk_last_updated_user = lu.us_id
			LEFT JOIN BugTracker.dbo.users au ON bt.tsk_assigned_to_user = au.us_id
			JOIN BugTracker.dbo.statuses st ON bt.tsk_status = st.st_id
;

--SELECT * FROM #WORKITEM_TASKS;

DELETE FROM WORKITEM_TASK;
GO

--ADD TO ACTUAL WORKITEM_TASK TABLE
INSERT INTO WORKITEM_TASK(
	WORKITEMID, TASK_NUMBER, ASSIGNEDRESOURCEID, ESTIMATEDSTARTDATE, ACTUALSTARTDATE, ACTUALENDDATE
	, PLANNEDHOURS, ACTUALHOURS, COMPLETIONPERCENT, STATUSID
	, [TITLE], [DESCRIPTION], SORT_ORDER, ARCHIVE
	, CREATEDBY, CREATEDDATE, UPDATEDBY, UPDATEDDATE
)
SELECT
	wi.WORKITEMID
	, wt.BT_TASKID AS TASK_NUMBER
	, ar.WTS_RESOURCEID AS ASSIGNEDRESOURCEID
	, wt.PLANNEDSTARTDATE AS ESTIMATEDSTARTDATE
	, wt.ACTUALSTARTDATE
	, wt.ACTUALENDDATE
	, wt.PLANNEDHOURS
	, wt.ACTUALHOURS
	, wt.COMPLETIONPERCENT
	, s.STATUSID
	, wt.[TITLE]
	, wt.[DESCRIPTION]
	, wt.SORT_ORDER
	, wt.ARCHIVE
	, cr.FIRST_NAME + '.' + cr.LAST_NAME AS CREATEDBY
	, wt.CREATEDDATE
	, ur.FIRST_NAME + '.' + ur.LAST_NAME AS UDPATEDBY
	, wt.UPDATEDDATE
FROM
	#WORKITEM_TASKS wt
		JOIN WORKITEM wi ON wt.BUGTRACKER_ID = wi.BUGTRACKER_ID
		LEFT JOIN WTS_RESOURCE ar ON wt.FIRST_NAME + '.' + wt.LAST_NAME = ar.USERNAME
		LEFT JOIN WTS_RESOURCE cr ON wt.CREATED_FIRST_NAME + '.' + wt.CREATED_LAST_NAME = cr.USERNAME
		LEFT JOIN WTS_RESOURCE ur ON wt.UPDATED_FIRST_NAME + '.' + wt.UPDATED_LAST_NAME = ur.USERNAME
		JOIN [STATUS] s ON wt.BT_STATUS = s.[STATUS]
EXCEPT
SELECT WORKITEMID, TASK_NUMBER, ASSIGNEDRESOURCEID, ESTIMATEDSTARTDATE, ACTUALSTARTDATE, ACTUALENDDATE
	, PLANNEDHOURS, ACTUALHOURS, COMPLETIONPERCENT, STATUSID
	, [TITLE], [DESCRIPTION], SORT_ORDER, ARCHIVE
	, CREATEDBY, CREATEDDATE, UPDATEDBY, UPDATEDDATE
FROM WORKITEM_TASK
;

GO

DROP TABLE #WORKITEM_TASKS
GO

SELECT COUNT(*) FROM WORKITEM_TASK;
--SELECT * FROM WORKITEM_TASK;