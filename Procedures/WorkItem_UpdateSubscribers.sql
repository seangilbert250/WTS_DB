USE WTS
GO

IF EXISTS (SELECT * FROM sysobjects WHERE id = OBJECT_ID(N'[WorkItem_UpdateSubscribers]')
AND OBJECTPROPERTY(id, N'IsProcedure') = 1)

DROP PROCEDURE [dbo].[WorkItem_UpdateSubscribers]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].[WorkItem_UpdateSubscribers]
	@WorkItemID int
AS
BEGIN
	DECLARE @count int = 0;
	DECLARE @assignedID int = 0;
	DECLARE @primDevID int = 0;
	DECLARE @secDevID int = 0;
	DECLARE @wiSubmittedByID int = 0;
	DECLARE @submittedByID int = 0;
	DECLARE @smeID int = 0;
	DECLARE @leadIATWID int = 0;
	DECLARE @leadResourceID int = 0;

	SELECT @count = COUNT(*) FROM WORKITEM WHERE WORKITEMID = @WorkItemID;

	IF (ISNULL(@count,0) = 0)
		RETURN;

	SELECT @assignedID = wi.ASSIGNEDRESOURCEID
		, @primDevID = wi.PRIMARYRESOURCEID
		, @secDevID = wi.SECONDARYRESOURCEID
		, @wiSubmittedByID = wi.SubmittedByID
		, @submittedByID = wr.SUBMITTEDBY
		, @smeID = wr.SMEID
		, @leadIATWID = wr.LEAD_IA_TWID
		, @leadResourceID = wr.LEAD_RESOURCEID
	FROM
		WORKITEM wi
			LEFT JOIN WORKREQUEST wr ON wi.WORKREQUESTID = wr.WORKREQUESTID
	WHERE 
		wi.WORKITEMID = @WorkItemID;

	INSERT INTO WORKITEM_SUBSCRIBER(WORKITEMID, WTS_RESOURCEID)
	SELECT DISTINCT WORKITEMID, WTS_RESOURCEID FROM (
		SELECT @WorkItemID AS WORKITEMID, @assignedID AS WTS_RESOURCEID UNION ALL
		SELECT @WorkItemID AS WORKITEMID, @primDevID AS WTS_RESOURCEID UNION ALL
		SELECT @WorkItemID AS WORKITEMID, @secDevID AS WTS_RESOURCEID UNION ALL
		SELECT @WorkItemID AS WORKITEMID, @wiSubmittedByID AS WTS_RESOURCEID UNION ALL
		SELECT @WorkItemID AS WORKITEMID, @submittedByID AS WTS_RESOURCEID UNION ALL
		SELECT @WorkItemID AS WORKITEMID, @smeID AS WTS_RESOURCEID UNION ALL
		SELECT @WorkItemID AS WORKITEMID, @leadIATWID AS WTS_RESOURCEID UNION ALL
		SELECT @WorkItemID AS WORKITEMID, @leadResourceID AS WTS_RESOURCEID
	) w_r
	WHERE w_r.WTS_RESOURCEID IS NOT NULL
	EXCEPT SELECT WORKITEMID, WTS_RESOURCEID FROM WORKITEM_SUBSCRIBER;

END;

GO
